allprojects {
	apply plugin: "eclipse"
	apply plugin: "idea"
	
	idea {
		module {
			outputDir file("build/classes/java/main")
			testOutputDir file("build/classes/java/test")
		}
	}
}

configure(subprojects) {
	apply plugin: "java-library"
	apply plugin: "maven-publish"
}

subprojects {
    beforeEvaluate {
    	[compileJava, compileTestJava]*.options*.encoding = "UTF-8"
    	
    	sourceCompatibility = 17
		targetCompatibility = 17
		
		ext.bootstrapMain = "muscaa.chess.bootstrap.main.Main"
		ext.setupVersion = "v1"
    	
        repositories {
			mavenCentral()
			maven { url "https://jitpack.io" }
			maven { url "https://libraries.minecraft.net" }
		}
		
		configurations {
			// required at compile & runtime, not included, not published
			provided {
				canBeResolved = true
			    canBeConsumed = true
			}
			
			// not required at compile & runtime, included, not published
			embedded {
				canBeResolved = true
			    canBeConsumed = true
			}
			
			// required at compile & runtime, included, published
		    export {
			    canBeResolved = true
			    canBeConsumed = true
		    }
		    
		    // required at compile & runtime, included, not published
		    internal {
			    canBeResolved = true
			    canBeConsumed = true
		    }
			
			// bootstrap dependencies
			bootstrap {
				canBeResolved = true
			    canBeConsumed = true
			}
		    
		    // shaded dependencies
		    shade {
				canBeResolved = true
			    canBeConsumed = true
			}
		    
		    compileClasspath.extendsFrom(provided)
		    runtimeClasspath.extendsFrom(provided)
		    testCompileClasspath.extendsFrom(provided)
		    testRuntimeClasspath.extendsFrom(provided)
		    
		    api.extendsFrom(export)
		    provided.extendsFrom(internal)
		    provided.extendsFrom(bootstrap)
		    provided.extendsFrom(shade)
		}
    }
    
    afterEvaluate {
        eclipse.project.name = "${projectName}-${subName}"
        
		jar {
			//archiveFileName.set("${subName}.jar")
			
			into("META-INF") {
	            from("${rootProject.projectDir}/LICENSE")
	            from("${rootProject.projectDir}/NOTICE")
	        }
	        
	        duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
			dependsOn configurations.shade
			from {
				configurations.shade.collect {
					it.isDirectory() ? it : zipTree(it)
				}
			}
		}
		
        def m2RepoDir = new File(System.getProperty("user.home"), ".m2/repository")
        def groupDir = new File(m2RepoDir, project.group.replace('.', '/'))
        def publishDir = new File(groupDir, "${project.name}/${project.version}")
		
		tasks.register("bundle", Zip) {
		    destinationDirectory.set(publishDir)
		    archiveFileName.set("bundle.zip")
			
			from(configurations.bootstrap)
		    into("libs") {
		    	from(tasks.named("jar"))
		    	from(configurations.embedded)
	        	from(configurations.export)
	        	from(configurations.internal)
		    }
		}
		
		tasks.register("setup") {
		    doLast {
	            def file = new File(publishDir, "setup.war")
	            file.text = setupVersion
		    }
		}
		
		tasks.named("publishToMavenLocal") {
		    finalizedBy "bundle", "setup"
		}
		
		java {
			withSourcesJar()
			withJavadocJar()
		}
		
		publishing {
			publications {
				maven(MavenPublication) {
					from components.java
				}
			}
		}
    }
}
