allprojects {
	apply plugin: "eclipse"
	apply plugin: "idea"
	
	idea {
		module {
			outputDir file("build/classes/java/main")
			testOutputDir file("build/classes/java/test")
		}
	}
}

configure(subprojects) {
	apply plugin: "java-library"
	apply plugin: "maven-publish"
}

subprojects {
    beforeEvaluate {
    	[compileJava, compileTestJava]*.options*.encoding = "UTF-8"
    	
    	sourceCompatibility = 17
		targetCompatibility = 17
    	
        repositories {
			mavenCentral()
			//maven { url "https://s01.oss.sonatype.org" }
			// You may want to remove the following line if you have errors downloading dependencies.
			//mavenLocal()
			//maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
			//maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
			maven { url "https://jitpack.io" }
		}
    }
    
    afterEvaluate {
        eclipse.project.name = "${projectName}-${subName}"
        
		jar {
			archiveFileName.set("${subName}.jar")
			
			into("META-INF") {
	            from("${rootProject.projectDir}/LICENSE")
	            from("${rootProject.projectDir}/NOTICE")
	        }
		}
		
		tasks.register("copyDependencies", Copy) {
		    from configurations.runtimeClasspath
		    into "${buildDir}/libs"
		}
		
		java {
			withSourcesJar()
			withJavadocJar()
		}
		
		publishing {
			publications {
				maven(MavenPublication) {
					groupId project.group
					artifactId project.name
					version project.version
					from components.java
				}
			}
		}
    }
}
